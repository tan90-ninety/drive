package com.atguigu.com

import com.atguigu.daijia.model.form.rules.FeeRuleRequest
import java.math.BigDecimal;

global com.atguigu.daijia.model.vo.rules.FeeRuleResponse feeRuleResponse;

/**
1.起步价
    00:00:00-06:59:59  19元(含3公里)
    07:00:00-23:59:59  19元(含5公里)
*/
rule "起步价 00:00:00-06:59:59  19元(含3公里)"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(startTime >= "00:00:00" && startTime <= "06:59:59")
    then
        feeRuleResponse.setBaseDistance(new BigDecimal("3.0"));
        feeRuleResponse.setBaseDistanceFee(new BigDecimal("19.0"));
end
rule "起步价 07:00:00-23:59:59  19元(含5公里)"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(startTime >= "07:00:00" && startTime <= "23:59:59")
    then
        feeRuleResponse.setBaseDistance(new BigDecimal("5.0"));
        feeRuleResponse.setBaseDistanceFee(new BigDecimal("19.0"));
end

/**
2.里程费
    超出起步里程后开始计算
    00:00:00-06:59:59   4元/1公里
    07:00:00-23:59:59   3元/1公里
*/
rule "里程费 00:00:00-06:59:59 4元/1公里"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(startTime >= "00:00:00" && startTime <= "06:59:59" && distance.doubleValue() > 3.0)
    then
        feeRuleResponse.setExceedDistancePrice(new BigDecimal("4.0"));
        BigDecimal exceedDistance = $rule.getDistance().subtract(new BigDecimal("3.0"));
        feeRuleResponse.setExceedDistance(exceedDistance);
end
rule "里程费 00:00:00-06:59:59 未超出起步里程"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(startTime >= "00:00:00" && startTime <= "06:59:59" && distance.doubleValue() <= 3.0)
    then
        feeRuleResponse.setExceedDistancePrice(new BigDecimal("4.0"));
        feeRuleResponse.setExceedDistance(new BigDecimal("0.0"));
end
rule "里程费 07:00:00-23:59:59 3元/1公里"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(startTime >= "07:00:00" && startTime <= "23:59:59" && distance.doubleValue() > 3.0)
    then
        feeRuleResponse.setExceedDistancePrice(new BigDecimal("3.0"));

        BigDecimal exceedDistance = $rule.getDistance().subtract(new BigDecimal("3.0"));
        feeRuleResponse.setExceedDistance(exceedDistance);
end
rule "里程费 07:00:00-23:59:59 未超出起步里程"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(startTime >= "07:00:00" && startTime <= "23:59:59" && distance.doubleValue() <= 3.0)
    then
        feeRuleResponse.setExceedDistancePrice(new BigDecimal("3.0"));
        feeRuleResponse.setExceedDistance(new BigDecimal("0.0"));
end

/**
3.等候费
    等候10分钟后  1元/1分钟
*/
rule "等候费 等候10分钟后 1元/1分钟"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(waitMinute > 10)
    then
        feeRuleResponse.setBaseWaitMinute(10);
        feeRuleResponse.setExceedWaitMinutePrice(new BigDecimal("1.0"));

        Integer exceedWaitMinute = $rule.getWaitMinute() - 10;
        feeRuleResponse.setExceedWaitMinute(exceedWaitMinute);
        System.out.println("等候费，超出分钟:" + feeRuleResponse.getExceedWaitMinute() + "分钟，单价：" + feeRuleResponse.getExceedWaitMinutePrice());
end
rule "无等候费"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(waitMinute <= 10)
    then
        feeRuleResponse.setBaseWaitMinute(10);
        feeRuleResponse.setExceedWaitMinutePrice(new BigDecimal("1.0"));

        feeRuleResponse.setExceedWaitMinute(0);
        System.out.println("等候费：无");
end


/**
4.远途费
    订单行程超出12公里后每公里1元
*/
rule "远途费 订单行程超出12公里后每公里1元"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(distance.doubleValue() > 12)
    then
        feeRuleResponse.setBaseLongDistance(new BigDecimal("12.0"));
        feeRuleResponse.setExceedLongDistancePrice(new BigDecimal("1.0"));

        BigDecimal exceedLongDistance = $rule.getDistance().subtract(new BigDecimal("12.0"));
        feeRuleResponse.setExceedLongDistance(exceedLongDistance);
end
rule "无远途费"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(distance.doubleValue() <= 12)
    then
        feeRuleResponse.setBaseLongDistance(new BigDecimal("12.0"));
        feeRuleResponse.setExceedLongDistancePrice(new BigDecimal("1.0"));

        feeRuleResponse.setExceedLongDistance(new BigDecimal("0.0"));
end

/**
5.计算总金额
    订单总金额 = 基础里程费 + 超出基础里程的费 + 等候费 + 远程费
*/
rule "计算总金额"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(distance.doubleValue() > 0.0)
    then
        BigDecimal distanceFee = feeRuleResponse.getBaseDistanceFee().add(feeRuleResponse.getExceedDistance().multiply(feeRuleResponse.getExceedDistancePrice()));
        BigDecimal waitFee = new BigDecimal(feeRuleResponse.getExceedWaitMinute()).multiply(feeRuleResponse.getExceedWaitMinutePrice());
        BigDecimal longDistanceFee = feeRuleResponse.getExceedLongDistance().multiply(feeRuleResponse.getExceedLongDistancePrice());
        BigDecimal totalAmount = distanceFee.add(waitFee).add(longDistanceFee);

        feeRuleResponse.setDistanceFee(distanceFee);
        feeRuleResponse.setWaitFee(waitFee);
        feeRuleResponse.setLongDistanceFee(longDistanceFee);
        feeRuleResponse.setTotalAmount(totalAmount);
        System.out.println("计算总金额:" + feeRuleResponse.getTotalAmount() + "元");
end
rule "计算0公里时总金额"
    salience 10
    no-loop true
    when
        $rule:FeeRuleRequest(distance.doubleValue() == 0.0)
    then
        BigDecimal distanceFee = new BigDecimal("0.0");
        BigDecimal waitFee = new BigDecimal(feeRuleResponse.getExceedWaitMinute()).multiply(feeRuleResponse.getExceedWaitMinutePrice());
        BigDecimal longDistanceFee = new BigDecimal("0.0");
        BigDecimal totalAmount = distanceFee.add(waitFee).add(longDistanceFee);

        feeRuleResponse.setDistanceFee(distanceFee);
        feeRuleResponse.setWaitFee(waitFee);
        feeRuleResponse.setLongDistanceFee(longDistanceFee);
        feeRuleResponse.setTotalAmount(totalAmount);
        System.out.println("计算总金额:" + feeRuleResponse.getTotalAmount() + "元");
end